plugins {
	id "org.springframework.boot" version '3.5.10'
	id "io.spring.dependency-management" version '1.1.7'
	id 'java'
	id 'jacoco'
	id "io.freefair.lombok" version "8.14.4"
    id "com.diffplug.spotless" version "8.2.1"
}

group = 'de.unimarburg.diz'
version = '0.0.2-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
}

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}

    snapshotTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.test.output
    }
}

configurations {
	intTestImplementation.extendsFrom implementation
	intTestRuntimeOnly.extendsFrom runtimeOnly

    snapshotTestImplementation.extendsFrom implementation
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	shouldRunAfter test
}

tasks.register('snapshotTest', Test) {
    description = 'Runs snapshot tests.'
    group = 'verification'

    testClassesDirs = sourceSets.snapshotTest.output.classesDirs
    classpath = sourceSets.snapshotTest.runtimeClasspath
    shouldRunAfter test
}

build.dependsOn integrationTest
build.dependsOn snapshotTest

ext {
	set('springCloudVersion', "2025.0.1")
    set('apacheCommonsCsv', "1.14.1")
}

dependencies {
	// spring cloud stream kafka
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.apache.httpcomponents:httpclient:4.5.14'
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
	implementation 'org.springframework.cloud:spring-cloud-stream'
	implementation 'org.springframework.retry:spring-retry'
	// spring boot
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// metrics
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-core'

	// mapper
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'


    implementation("org.apache.commons:commons-csv:${apacheCommonsCsv}")

	// unit tests
	testImplementation 'org.springframework.cloud:spring-cloud-stream-test-binder'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.assertj:assertj-core'


	// integration tests
	intTestImplementation 'org.springframework.kafka:spring-kafka-test'
	intTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	intTestImplementation 'org.assertj:assertj-core'

    // snapshot tests
    snapshotTestImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    snapshotTestImplementation 'org.assertj:assertj-core'
    snapshotTestImplementation "com.approvaltests:approvaltests:26.7.1"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
    dependsOn(tasks.spotlessCheck)
}

integrationTest {
	useJUnitPlatform()
    dependsOn(tasks.spotlessCheck)
}

snapshotTest {
    useJUnitPlatform()
    dependsOn(tasks.spotlessCheck)
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

jacoco {
	toolVersion = "0.8.12"
	reportsDirectory = layout.buildDirectory.dir('jacoco-reports')
}

jacocoTestReport {
	reports {
		xml.required = true
		csv.required = true
		csv.outputLocation = layout.buildDirectory.file('jacoco/coverage.csv')
		html.outputLocation = layout.buildDirectory.dir('jacoco/html')
	}
}

jar {
	enabled = false
}

bootJar {
	mainClass.set('de.unimarburg.diz.kafkagenetictomtbxml.Application')
}
